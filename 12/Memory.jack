// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {
    static int base;
    static int term;
    static int freeMem;

    /** Initializes the class. */
    function void init() {
        let base = 2048;
        let term = 16383;
		
		//set up the heap pointer for usage
        let freeMem = base;
        let freeMem[0] = term - base;
		
		//next
        let freeMem[1] = null;
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
		return address[0];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
		let address[0] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
		//allocated mem has to be positive!
		if (size < 0) { do Sys.error(5); }

		return Memory.allocFirstFit(size);
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
		var Array last, current;
		var Array next;
		var boolean gap;

		// last block?
		let next = o - 1; 

		// Check to see if the list is empty and create root
		if (freeMem = 0) {
			let freeMem = next;
			let freeMem[1] = 0;
			return;
		}
		
		//search for gap
		let last = freeMem;
		let current = freeMem[1];
		let gap = false;

		//locate gap, already sorted
		while ((~(current = 0)) & (~gap)) {
			if (~(current[1] = 0)) {
				if (next < current[1]) {
					let last = current;
					let current = current[1];
				}
			} else {
				let gap = true;
			}
		}

		//free
		let last[1] = next;
		let next[1] = current;

		return;
    }    
	
	function int allocFirstFit(int size) {
		var Array last, current;
		var Array next;

		let last = null;
		let current = freeMem;

		// Alloc the whole block.
		while (~(current = 0)) {
			if ((current[0] - 1) = size) { 
				do Memory.deleteBlock(last, current);
				return current + 1;
			}

			if (current[0] > (size + 2)) {
				let current[(current[0] - 1) - size] = size + 1;
				let next = current + current[0] - size;
				let current[0] = current[0] - size - 1;
				return next;
			}

			// Go to the next block:
			let last = current;
			let current = current[1];
		}

		// If we get here, it's because no memory could be allocated.
		do Sys.error(6); // 6 Memory.alloc Heap overflow
		return null;
    }
	
	function void deleteBlock(int last, int current) {
		//reset, previous is next
		if (last = 0) {
			let freeMem = current[1];
		} else {
			let last[1] = current[1];
		}
		return;
	}
}
