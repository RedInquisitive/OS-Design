// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=last, out=next);
	
	Mux16(a=last, b=next, sel=inc, out=count);
	Mux16(a=count, b=in, sel=load, out=write);
	Mux16(a=write, b=false, sel=reset, out=save);
	
	Register(in=save, load=true, out=out, out=last);
}
